plugins {
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'org.openapi.generator' version '7.2.0'
}

dependencies {
    implementation project(':commons')
    
    // Test dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'io.rest-assured:rest-assured:5.4.0'
    testImplementation 'org.awaitility:awaitility:4.2.0'
    
    // Generated client dependencies
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework:spring-webflux'
    implementation 'io.projectreactor.netty:reactor-netty'
}

openApiGenerate {
    generatorName = 'java'
    inputSpec = "$rootDir/openapi/data-product-api.yaml".toString()
    outputDir = "$buildDir/generated".toString()
    apiPackage = 'com.dataproduct.e2e.generated.api'
    modelPackage = 'com.dataproduct.e2e.generated.model'
    configOptions = [
        library: 'webclient',
        dateLibrary: 'java8',
        java8: 'true',
        hideGenerationTimestamp: 'true',
        useJakartaEe: 'true'
    ]
}

sourceSets {
    main {
        java {
            srcDirs += "$buildDir/generated/src/main/java"
        }
    }
}

compileJava.dependsOn tasks.openApiGenerate

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
    
    systemProperty 'spring.profiles.active', 'e2e'
    systemProperty 'api.base.url', project.findProperty('api.base.url') ?: 'http://localhost:8080'
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}